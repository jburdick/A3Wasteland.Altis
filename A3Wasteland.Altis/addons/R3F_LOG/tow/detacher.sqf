/**
 * D�tacher un objet d'un v�hicule
 * 
 * @param 0 l'objet � d�tacher
 * 
 * Copyright (C) 2014 Team ~R3F~
 * 
 * This program is free software under the terms of the GNU General Public License version 3.
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

if (R3F_LOG_mutex_local_verrou) then
{
	hintC STR_R3F_LOG_mutex_action_en_cours;
}
else
{
	R3F_LOG_mutex_local_verrou = true;
	
	private ["_tow", "_object"];
	
	_object = _this select 0;
	_tow = _object getVariable "R3F_LOG_est_transporte_par";
	
	// Ne pas permettre de d�crocher un objet s'il est en fait h�liport�
	if (_tow getVariable "R3F_LOG_fonctionnalites" select R3F_LOG_IDX_can_tow) then
	{
		[_object, player] call R3F_LOG_FNCT_define_owner_lock;
		
		player switchMove "AinvPknlMstpSlayWrflDnon_medic";

		_towerBB = _tow call fn_boundingBoxReal;
		_towerMinBB = _towerBB select 0;
		_towerMaxBB = _towerBB select 1;

		if ((getPosASL player) select 2 > 0) then
		{
			player attachTo [_tow,
			[
				(_towerMinBB select 0) - 0.25,
				(_towerMinBB select 1) - 0.25,
				_towerMinBB select 2
			]];

			player setDir 90;
			player setPos (getPos player);
			sleep 0.05;
			detach player;
		};

		sleep 2;

		// On m�morise sur le r�seau que le v�hicule remorque quelque chose
		_tow setVariable ["R3F_LOG_remorque", objNull, true];
		// On m�morise aussi sur le r�seau que le objet est attach� en remorque
		_object setVariable ["R3F_LOG_est_transporte_par", objNull, true];

		if (local _object) then
		{
			[_object] call detachTowedObject;
		}
		else
		{
			pvar_detachTowedObject = [netId _object];
			publicVariable "pvar_detachTowedObject";
		};

		sleep 4;

		player switchMove "";
		
		if (alive player) then
		{
			if (_object getVariable "R3F_LOG_fonctionnalites" select R3F_LOG_IDX_can_be_moved_by_player) then
			{
				// Si personne n'a touch� � l'objet pendant le sleep 7
				if (isNull (_tow getVariable "R3F_LOG_remorque") &&
					(isNull (_object getVariable "R3F_LOG_est_transporte_par")) &&
					(isNull (_object getVariable "R3F_LOG_est_deplace_par"))
				) then
				{
					[_object, player, 0, true] spawn R3F_LOG_FNCT_object_move;
				};
			}
			else
			{
				systemChat STR_R3F_LOG_action_detacher_fait;
			};
		};
	}
	else
	{
		hintC STR_R3F_LOG_action_detacher_impossible_pour_ce_vehicule;
	};
	
	R3F_LOG_mutex_local_verrou = false;
};